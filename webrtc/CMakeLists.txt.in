cmake_minimum_required(VERSION 3.3)
project(webrtc)

find_package(Git REQUIRED)
if (WIN32)
  set(PYTHON_EXECUTABLE ${DEPOT_TOOLS_PATH}/python.bat)
else (WIN32)
  find_package(PythonInterp 2.7 REQUIRED)
endif (WIN32)

include(DepotTools)
include(GClient)
include(Environment)
include(LibWebRTCExecute)

if (WEBRTC_REVISION)
  libwebrtc_execute(
      COMMAND ${GIT_EXECUTABLE} checkout ${WEBRTC_REVISION}
      OUTPUT_VARIABLE _WEBRTC_CHECKOUT
      WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
      STAMPFILE webrtc-checkout-commit
      STATUS "Checking out webrtc to commit ${WEBRTC_REVISION}"
      ERROR "Unable to checkout webrtc to commit ${WEBRTC_REVISION}"
  )
elseif (WEBRTC_BRANCH_HEAD)
  libwebrtc_execute(
      NAME webrtc-sync
      COMMAND ${GCLIENT_EXECUTABLE} sync --force
      WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
      COMMENT "Synchronizing WebRTC"
      STATUS "Synchronizing WebRTC in ${CMAKE_SOURCE_DIR}"
  )

  libwebrtc_execute(
      COMMAND ${GIT_EXECUTABLE} checkout -b m84 refs/remotes/branch-heads/4147
      OUTPUT_VARIABLE _WEBRTC_CONFIG_FETCH
      WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
      STAMPFILE webrtc-config-fetch
      STATUS "Setting up branch-heads refspecs"
      ERROR "Unable to add branch-heads refspec to the git config"
  )
endif (WEBRTC_REVISION)

include(LibWebRTCCommand)

libwebrtc_command(
  NAME webrtc-sync
  COMMAND ${GCLIENT_EXECUTABLE} sync --force
  WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
  COMMENT "Synchronizing WebRTC"
  STATUS "Synchronizing WebRTC in ${CMAKE_SOURCE_DIR}"
)

if (UNIX AND NOT APPLE)
  set(SYSROOT_ARCH ${TARGET_CPU})

  if (SYSROOT_ARCH STREQUAL "x64")
    set(SYSROOT_ARCH "amd64")
  elseif (SYSROOT_ARCH STREQUAL "x86")
    set(SYSROOT_ARCH "i386")
  endif (SYSROOT_ARCH STREQUAL "x64")

  libwebrtc_command(
      NAME webrtc-toolchain
      COMMAND ${PYTHON_EXECUTABLE} ${WEBRTC_PARENT_DIR}/src/build/linux/sysroot_scripts/install-sysroot.py --arch=${SYSROOT_ARCH}
      WORKING_DIRECTORY "${WEBRTC_PARENT_DIR}"
      COMMENT "Retrieving sysroot"
      DEPENDS webrtc-sync
  )
  set(_DEPENDENCIES ${_DEPENDENCIES} webrtc-toolchain)

  set(_PLATFORM linux*)
  set(_FOLDER linux64)
elseif (APPLE)
  set(_PLATFORM darwin)
  set(_FOLDER mac)
elseif (WIN32)
  libwebrtc_command(
      NAME webrtc-toolchain
      COMMAND ${PYTHON_EXECUTABLE} src/build/vs_toolchain.py update
      WORKING_DIRECTORY "${WEBRTC_PARENT_DIR}"
      COMMENT "Retrieving Visual Studio toolchain"
      DEPENDS webrtc-sync
  )
  set(_DEPENDENCIES ${_DEPENDENCIES} webrtc-toolchain)

  set(_PLATFORM win32)
  set(_FOLDER win)
  set(_SUFFIX .exe)
  set(_SCRIPT_SUFFIX .bat)
endif (UNIX AND NOT APPLE)

include(Gn)

libwebrtc_command(
    NAME webrtc-generate
    COMMAND ${_GEN_COMMAND}
    WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
    COMMENT "Generating build files"
    STATUS "Generating build files with ${_GEN_COMMAND}"
    DEPENDS ${_DEPENDENCIES}
)

set(_NINJA_COMMAND ninja ${NINJA_ARGS} -C ${_NINJA_BUILD_DIR} :webrtc jsoncpp libyuv)

libwebrtc_command(
    NAME webrtc-build
    COMMAND ${_NINJA_COMMAND}
    WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}"
    COMMENT "Running ninja"
    STATUS "Running ninja build ${_NINJA_COMMAND}"
    DEPENDS webrtc-generate
)
